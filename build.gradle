plugins {
	id 'maven-publish'
	id 'org.quiltmc.loom'
	id 'me.modmuss50.mod-publish-plugin' version '0.5.2'
}

base {
	archivesName = project.archives_base_name
}

var modVersion = property("mod.version")
var mcVersion = property("deps.minecraft")
version = "${modVersion}+${mcVersion}"
group = project.maven_group

var hasUi = stonecutter.compare(mcVersion, "1.19.4") >= 0;
var awVersion =
		stonecutter.compare(mcVersion, "1.20.2") >= 0 ? "1.20.2"
		: stonecutter.compare(mcVersion, "1.20.1") >= 0 ? "1.20.1"
		: stonecutter.compare(mcVersion, "1.19.4") >= 0 ? "1.19.4"
		: stonecutter.compare(mcVersion, "1.19.3") >= 0 ? "1.19.3"
		: stonecutter.compare(mcVersion, "1.19.2") >= 0 ? "1.19.2"
		: stonecutter.compare(mcVersion, "1.19") >= 0 ? "1.19.0"
		: stonecutter.compare(mcVersion, "1.17.1") >= 0 ? "1.17.1" : "1.16.5";

stonecutter.const("hasUi", hasUi)

if (stonecutter.current.isActive) {
	rootProject.tasks.register('client') {
		group 'project'
		dependsOn tasks.named('runClient')
	}
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	maven {
		name "Modrinth"
		url "https://api.modrinth.com/maven"
	}

	// for some reason using the terraformers maven version of
	// modmenu breaks quilt loader.
	// maven {
	// 	name "Terraformers"
	// 	url "https://maven.terraformersmc.com"
	// }
}

if (hasUi) {
	sourceSets.main.java {
		srcDirs += ['src/ui/java']
	}
}

loom {
	// Loom and Loader both use this block in order to gather more information about your mod.
	mods {
		// This should match your mod id.
		"skin_overrides" {
			// Tell Loom about each source set used by your mod here. This ensures that your mod's classes are properly transformed by Loader.
			sourceSet sourceSets.main

			// If you shade (directly include classes, not JiJ) a dependency into your mod, include it here using one of these methods:
			// dependency("com.example.shadowedmod:1.2.3")
			// configuration("exampleShadedConfigurationName")
		}
	}

	accessWidenerPath = getRootProject().file("src/main/resources/aw/${awVersion}.accesswidener")
}

dependencies {
	minecraft "com.mojang:minecraft:${mcVersion}"
	modImplementation "org.quiltmc:quilt-loader:${property("deps.quilt_loader")}"

	if (!property("deps.quilt_mappings").contains(":")) {
		mappings "org.quiltmc:quilt-mappings:${property("deps.quilt_mappings")}:intermediary-v2"
	} else {
		mappings property("deps.quilt_mappings")
	}

	// QSL is not a complete API; You will need Quilted Fabric API to fill in the gaps.
	// Quilted Fabric API will automatically pull in the correct QSL version.
	// modImplementation libs.quilted.fabric.api
	// modImplementation libs.bundles.quilted.fabric.api // If you wish to use Fabric API's deprecated modules, you can replace the above line with this one

	modImplementation "maven.modrinth:modmenu:${property("deps.modmenu")}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${property("deps.fabric_api")}"

	modRuntimeOnly "maven.modrinth:ears:${property("deps.ears")}"
}

processResources {
	inputs.properties 'version': version,
			'group': project.group,
			'minecraft_version': mcVersion,
			'access_widener': awVersion,
			'modmenu_entrypoint': hasUi ? 'modmenu' : ''

	filesMatching(['quilt.mod.json', 'fabric.mod.json']) {
		expand 'version': version,
				'group': project.group,
				'minecraft_version': mcVersion,
				'access_widener': awVersion,
				'modmenu_entrypoint': hasUi ? 'modmenu' : ''
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Still required by IDEs such as Eclipse and Visual Studio Code
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	// withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	// withJavadocJar()
}

jar {
	from getRootProject().file('LICENSE')

	exclude {
		it.name.endsWith('.accesswidener') && it.name != "${awVersion}.accesswidener"
	}
}

publishMods {
	displayName = "skin overrides ${modVersion} for ${mcVersion}"
	file = remapJar.archiveFile
	changelog = rootProject.file('CHANGELOG.md').getText()
	type = STABLE

	modLoaders.add('quilt')
	modLoaders.add('fabric')

	dryRun = !providers.environmentVariable('MODRINTH_TOKEN').isPresent()
			|| property("pub.should_publish") == 'false'

	var mcVersions = [mcVersion]
	mcVersions.addAll(property("pub.additional_versions").toString().split(' ').findAll { !it.empty })

	modrinth {
		projectId = 'GON0Fdk5'
		accessToken = providers.environmentVariable('MODRINTH_TOKEN')
		mcVersions.forEach(minecraftVersions::add)

		requires 'fabric-api'
		optional 'modmenu'
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
